buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url "https://repo.spongepowered.org/repository/maven-public/" }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: "org.spongepowered.mixin"

version = "${mod_version}"
group = 'dev.toma.pubgmc'
archivesBaseName = 'pubgmc'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run/client')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'fml.coreMods.load', 'dev.toma.pubgmc.mixin.PubgmcLaunchPlugin,zone.rong.mixinbooter.MixinBooterPlugin'
            properties 'mixin.env.remapRefMap': 'true'
            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"
            property "mixin.debug", 'true'
            arg "-mixin.config=mixins.pubgmc.json"
            arg '--username=User1'
        }

        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'fml.coreMods.load', 'dev.toma.pubgmc.mixin.PubgmcLaunchPlugin,zone.rong.mixinbooter.MixinBooterPlugin'
            properties 'mixin.env.remapRefMap': 'true'
            property 'mixin.env.refMapRemappingFile', "${project.projectDir}/build/createSrgToMcp/output.srg"
            property "mixin.debug", 'true'
            arg "-mixin.config=mixins.pubgmc.json"
            arg 'nogui'
        }
    }
}

repositories {
    maven { url "https://www.cursemaven.com" }
    maven { url 'https://maven.cleanroommc.com' }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    // Configuration lib
    compile "curse.maven:configuration-444699:${configuration_version}"

    // JourneyMap API
    compile group: "info.journeymap", name: "journeymap-api", version: "${journeymap_version}", changing: true

    // Mixins
    compile fg.deobf('zone.rong:mixinbooter:9.3')
    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'
}

sourceSets {
    main {
        ext.refMap = "mixins.pubgmc.refmap.json"
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"        : "PUBGMC",
                "Specification-Vendor"       : "Toma",
                "Specification-Version"      : "1",
                "Implementation-Title"       : project.name,
                "Implementation-Version"     : "${version}",
                "Implementation-Vendor"      : "Toma",
                "Implementation-Timestamp"   : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "FMLCorePlugin"              : "dev.toma.pubgmc.mixin.PubgmcLaunchPlugin",
                "FMLCorePluginContainsFMLMod": "true",
                "FMLAT"                      : "accesstransformer.cfg",
                "TweakClass"                 : "org.spongepowered.asm.launch.MixinTweaker",
                "MixinConfigs"               : "mixins.pubgmc.json",
                "ForceLoadAsMod"             : "true"
        ])
    }
}

jar.finalizedBy('reobfJar')

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

// This fixes assets loading in development env. Probably needed only for Intellij IDE
apply plugin: 'idea'
idea {
    module {
        inheritOutputDirs = true
        downloadJavadoc = true
        downloadSources = true
    }
}
subprojects {
    apply plugin: 'idea'
}

task prepareAssets(type: Copy) {
    group = 'build'
    from project.file('src/main/resources')
    into project.file('build/classes/java/main')
}

classes.dependsOn(prepareAssets)